@ECHO OFF
:: Set environment variable BSAUTO_HOME to the directory in which the programs and format
::   files reside.
:: Set environment variable BSAUTO_DATA to the directory in which the control file and
::   data files reside.
::
SET BSAUTO_HOME=c:\tmp\FH_CreditCard\src\BSauto
SET BSAUTO_DATA=%BSAUTO_HOME%\data
::
::Append the path at which advscore is located
SET PATH=%PATH%;c:\dac\fuhwa\CreditCard\prog\advanceRiskModel\release_nv
CD %BSAUTO_DATA%

:: Set a lock to prevent the same batch run at the same time
IF EXIST ".lock" GOTO LOCK
TYPE NUL >.lock

:: Check if the control file was given
IF "%1"=="" GOTO DEFAULT
SET CTLFILE=%1
GOTO PROCESS

:DEFAULT
SET CTLFILE=%BSAUTO_DATA%\FHBSAUTO.CTL

:PROCESS
ECHO The control file is %CTLFILE%
IF NOT EXIST "%CTLFILE%" GOTO NOTFOUND

:: del result generated by previous runs quietly
del /Q PROFILE*.csv
del /Q PD*.csv

:: Execute automation program
:: change the options
:: -u user_name -p password -s sql_server_name -d database
..\fhbsauto -u sa -p Emily1013 -s oliver\daisy -d fw_auto

IF ERRORLEVEL 12 GOTO ERROR12
IF ERRORLEVEL 11 GOTO ERROR11
IF ERRORLEVEL 10 GOTO ERROR10
IF ERRORLEVEL 9 GOTO ERROR9
IF ERRORLEVEL 8 GOTO ERROR8
IF ERRORLEVEL 7 GOTO ERROR7
IF ERRORLEVEL 6 GOTO ERROR6
IF ERRORLEVEL 5 GOTO ERROR5
IF ERRORLEVEL 4 GOTO ERROR4
IF ERRORLEVEL 3 GOTO ERROR3
IF ERRORLEVEL 2 GOTO ERROR2
IF ERRORLEVEL 1 GOTO ERROR1

:: FTP result to host
:: Create the temporary script file
:: Change the user name
:: Change the password
:: Change the working directory if needed
> script.ftp ECHO ftpguest@decision-analytics.com
>>script.ftp ECHO gonzalez
::>>script.ftp ECHO cd files/pictures
>>script.ftp ECHO ascii
>>script.ftp ECHO prompt n
>>script.ftp ECHO mput PD*.csv
>>script.ftp ECHO mput PROFILE*.csv
>>script.ftp ECHO put  BCRESULT.CTL
>>script.ftp ECHO quit
:: Use the temporary script for unattended FTP
FTP -v -s:script.ftp ftp.decision-analytics.com
:: For the paranoid: overwrite the temporary file before deleting it
TYPE NUL >script.ftp
DEL script.ftp

ECHO *** Success!! > FHBSAUTO.LOG
GOTO DELLOCK

:NOTFOUND
ECHO Control file not fount > FHBSAUTO.LOG
GOTO DELLOCK

:ERROR1
ECHO Control file does not exist. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR2
ECHO Statement file does not exist. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR3
ECHO Account file does not exist. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR4
ECHO Statement - number of records mismatch between text file and control file. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR5
ECHO Account - number of records mismatch between text file and control file. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR6
ECHO Statement - number of records mismatch between text file and temp table. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR7
ECHO Account - number of records mismatch between text file and temp table. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR8
ECHO Account - number of records mismatch between text and cycle date loaded. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR9
ECHO Statement - number of records mismatch between temp and production table. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR10
ECHO Account - number of records mismatch between temp and production table. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR11
ECHO SQL runtime error. > FHBSAUTO.LOG
GOTO DELLOCK
:ERROR12
ECHO System command error. > FHBSAUTO.LOG
GOTO DELLOCK

:LOCK
ECHO The same job is currently running, you have to wait until it completes. > FHBSAUTO.LOG
GOTO END

:DELLOCK
DEL .lock

:END